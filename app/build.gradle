apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)
    buildToolsVersion BUILDTOOLS_VERSION

    defaultConfig {
        applicationId "com.bamboolmc.zhiqu"
        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    //签名
    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile file("/Users/limc/StudioProjects/ProjectsJks/ZhiQu.jks")
            storePassword "zhiqu&pass"
            keyAlias "zhiquApplication"
            keyPassword "zhiqu&pass"
        }
    }

    buildTypes {
        debug {
//            //混淆
//            minifyEnabled true
//            //加载默认混淆配置文件
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            //不显示log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            //加载默认混淆配置文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为zhiqu_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "zhiqu_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }


        }
    }

    //友盟统计打包
    productFlavors {
        xiaomi {}
        wandoujia {}
        tencent {}
        taobao {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile project(':Library')
    if (!IsDebugModuleNews.toBoolean()) {
        compile project(':modulenews')
    }
    if (!IsDebugModuleVideo.toBoolean()) {
        compile project(':modulevideo')
    }

    if (!IsDebugModuleWeather.toBoolean()) {
        compile project(':moduleweather')
    }

    annotationProcessor "com.alibaba:arouter-compiler:${arouter_compiler_version}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_compiler_version}"

    //RxBus
    compile 'com.hwangjr.rxbus:rxbus:1.0.5'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.5'
    compile 'com.google.dagger:dagger:2.5'

    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'

    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.google.code.gson:gson:2.6.1'

    compile 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    compile 'com.ogaclejapan.smarttablayout:utils-v4:1.6.1@aar'

    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'

    compile 'io.reactivex:rxjava:1.2.4'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.google.guava:guava:18.0'

    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    compile 'cn.bingoogolapple:bga-badgeview:1.0.2@aar'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.15'

    compile 'com.cesards.android:cropimageview:1.0.2.1'

}
